#version 330 core

out vec4 FragColor;

in VS_OUT{
    vec3 FragPos;
    vec3 Normal;
    vec2 TexCoords;
    vec3 TangentLightPos;
    vec3 TangentViewPos;
    vec3 TangentFragPos;
} fs_in;

uniform sampler2D diffuseMap;
uniform sampler2D normalMap;
uniform sampler2D depthMap;

uniform vec3 lightPos;
uniform vec3 viewPos;

uniform bool blinn_phong;
uniform bool normal_mapping_flag;
uniform bool paralax_flag;
uniform float height_scale;

vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir)
{ 
    // количество слоев глубины
    const float minLayers = 4;
    const float maxLayers = 16;
    float numLayers = mix(maxLayers, minLayers, abs(dot(vec3(0.0, 0.0, 1.0), viewDir)));  
    // вычисляем размер каждого слоя
    float layerDepth = 1.0 / numLayers;
    // глубина текущего слоя
    float currentLayerDepth = 0.0;
    // величина сдвига текстурных координат для каждого слоя (из вектора P)
    vec2 P = viewDir.xy / viewDir.z * height_scale; 
    vec2 deltaTexCoords = P / numLayers;
  
    // получаем начальные значения
    vec2  currentTexCoords     = texCoords;
    float currentDepthMapValue = texture(depthMap, currentTexCoords).r;
      
    while(currentLayerDepth < currentDepthMapValue)
    {
        // сдвигаем текстурные координаты вдоль направления вектора P
        currentTexCoords -= deltaTexCoords;
        // получаем значение глубины из карты глубины для текущих текстурных координат
        currentDepthMapValue = texture(depthMap, currentTexCoords).r;  
        // получаем значение глубины следующего слоя
        currentLayerDepth += layerDepth;  
    }
    
    return currentTexCoords;
}

vec2 calc_tex_coords(vec3 view_dir) {
    if (paralax_flag) {
        vec2 tex_coords = ParallaxMapping(fs_in.TexCoords,  view_dir);
        if(tex_coords.x > 1.0 || tex_coords.y > 1.0 || tex_coords.x < 0.0 || tex_coords.y < 0.0) {
            discard;
        }

        return tex_coords;
    } else {
        return fs_in.TexCoords;
    }
}

vec3 calc_normal(vec2 tex_coords) {
    vec3 normal;

    if (normal_mapping_flag) {
        normal = texture(normalMap, tex_coords).rgb;
        normal = normalize(normal * 2.0 - 1.0);
    } else {
        normal = normalize(fs_in.Normal);
    }

    return normal;
}

vec3 calc_light_dir() {
    vec3 light_dir;

    if (normal_mapping_flag) {
        light_dir = normalize(fs_in.TangentLightPos - fs_in.TangentFragPos);
    } else {
        light_dir = normalize(lightPos - fs_in.FragPos);
    }

    return light_dir;
}

vec3 calc_view_dir() {
    vec3 view_dir;

    if (normal_mapping_flag) {
        view_dir = normalize(fs_in.TangentViewPos - fs_in.TangentFragPos);
    } else {
        view_dir = normalize(viewPos - fs_in.FragPos);
    }

    return view_dir;
}

float calc_spec(vec3 light_dir, vec3 view_dir, vec3 normal) {
    float spec;

    if (blinn_phong) {
        vec3 halfway_dir = normalize(light_dir + view_dir);
        spec = pow(max(dot(normal, halfway_dir), 0.0), 32.0);
    } else {
        vec3 reflect_dir = reflect(-light_dir, normal);
        spec = pow(max(dot(view_dir, reflect_dir), 0.0), 8.0);
    }

    return spec;
}


void main()
{
    vec3 view_dir = calc_view_dir();
    vec2 tex_coords = calc_tex_coords(view_dir);
    vec3 normal = calc_normal(tex_coords);
    
    // получаем диффузный цвет
    vec3 color = texture(diffuseMap, tex_coords).rgb;

    // фоновая составляющая
    vec3 ambient = 0.1 * color;

    // диффузная составляющая
    vec3 light_dir = calc_light_dir();
    float diff = max(dot(light_dir, normal), 0.0);
    vec3 diffuse = diff * color;

    // отраженная составляющая
    vec3 reflect_dir = reflect(-light_dir, normal);
    float spec = calc_spec(light_dir, view_dir, normal);
    vec3 specular = vec3(0.2) * spec; // подразумеваем яркий белый 

    FragColor = vec4(ambient + diffuse + specular, 1.0);
}
